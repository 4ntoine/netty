plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'maven-publish'
}

android {
  compileSdkVersion 29
  buildToolsVersion "30.0.2"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  externalNativeBuild {
    cmake {
      path 'CMakeLists.txt'
    }
  }
}

dependencies {
  api 'io.netty:netty-transport-native-epoll:4.1.55.Final'
}

// `./gradlew publishToMavenLocal` to publish

publishing {
    publications {
        transport_native_epoll(MavenPublication) {
            artifact("$buildDir/outputs/aar/transport-native-epoll-release.aar") {
              group 'io.netty'
              artifactId 'netty-transport-native-epoll-android'
              version = '4.1.55.Final'
            }
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null)
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

publishToMavenLocal.dependsOn assemble
